#!/usr/bin/env gosh
(add-load-path "./src")
(use isl.compiler.frontend)
(use isl.compiler.codegen)

(define (usage)
  (display "Usage: islc-llvm [--profile strict|extended] [--mode module|aot] [-o output.ll] <file> [file ...]\n"))

(define (starts-with? s prefix)
  (let ((n (string-length s))
        (m (string-length prefix)))
    (and (>= n m)
         (string=? (substring s 0 m) prefix))))

(define (parse-args args)
  (let loop ((xs args) (profile 'extended) (mode 'module) (out #f) (files '()))
    (if (null? xs)
        (list 'ok profile mode out (reverse files))
        (let ((a (car xs)))
          (cond
           ((or (string=? a "-h") (string=? a "--help"))
            (list 'help))
           ((string=? a "--profile")
            (if (null? (cdr xs))
                (list 'error "--profile requires strict or extended")
                (loop (cddr xs) (cadr xs) mode out files)))
           ((starts-with? a "--profile=")
            (loop (cdr xs) (substring a 10 (string-length a)) mode out files))
           ((string=? a "--mode")
            (if (null? (cdr xs))
                (list 'error "--mode requires module or aot")
                (loop (cddr xs) profile (string->symbol (cadr xs)) out files)))
           ((starts-with? a "--mode=")
            (loop (cdr xs) profile (string->symbol (substring a 7 (string-length a))) out files))
           ((string=? a "-o")
            (if (null? (cdr xs))
                (list 'error "-o requires output path")
                (loop (cddr xs) profile mode (cadr xs) files)))
           (else
            (loop (cdr xs) profile mode out (cons a files))))))))

(define (main argv)
  (let ((parsed (parse-args (cdr argv))))
    (case (car parsed)
      ((help)
       (usage)
       0)
      ((error)
       (usage)
       (display "Error: ")
       (display (cadr parsed))
       (newline)
       2)
      ((ok)
       (let ((profile (cadr parsed))
             (mode (caddr parsed))
             (out (cadddr parsed))
             (files (car (cddddr parsed))))
         (if (null? files)
             (begin
               (usage)
               2)
             (let ((env (make-frontend-env profile)))
               (let loop ((xs files) (all-units '()))
                 (if (null? xs)
                     (let ((llvm (if (eq? mode 'aot)
                                     (ll-units->llvm-aot-module (reverse all-units))
                                     (ll-units->llvm-module (reverse all-units)))))
                       (if out
                           (call-with-output-file out
                             (lambda (p)
                               (display llvm p))
                             :if-exists :supersede)
                           (display llvm))
                       0)
                     (let ((units (frontend-compile-file (car xs) env)))
                       (loop (cdr xs) (append (reverse units) all-units)))))))))
      (else
       (usage)
       2))))

(exit (main (command-line)))
