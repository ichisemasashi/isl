#!/usr/bin/env gosh
(add-load-path "./src")
(use isl.compiler.frontend)
(use isl.compiler.codegen)

(define (usage)
  (display "Usage: islc-llvm [--profile strict|extended] [-o output.ll] <file> [file ...]\n"))

(define (starts-with? s prefix)
  (let ((n (string-length s))
        (m (string-length prefix)))
    (and (>= n m)
         (string=? (substring s 0 m) prefix))))

(define (parse-args args)
  (let loop ((xs args) (profile 'extended) (out #f) (files '()))
    (if (null? xs)
        (list 'ok profile out (reverse files))
        (let ((a (car xs)))
          (cond
           ((or (string=? a "-h") (string=? a "--help"))
            (list 'help))
           ((string=? a "--profile")
            (if (null? (cdr xs))
                (list 'error "--profile requires strict or extended")
                (loop (cddr xs) (cadr xs) out files)))
           ((starts-with? a "--profile=")
            (loop (cdr xs) (substring a 10 (string-length a)) out files))
           ((string=? a "-o")
            (if (null? (cdr xs))
                (list 'error "-o requires output path")
                (loop (cddr xs) profile (cadr xs) files)))
           (else
            (loop (cdr xs) profile out (cons a files))))))))

(define (main argv)
  (let ((parsed (parse-args (cdr argv))))
    (case (car parsed)
      ((help)
       (usage)
       0)
      ((error)
       (usage)
       (display "Error: ")
       (display (cadr parsed))
       (newline)
       2)
      ((ok)
       (let ((profile (cadr parsed))
             (out (caddr parsed))
             (files (cadddr parsed)))
         (if (null? files)
             (begin
               (usage)
               2)
             (let ((env (make-frontend-env profile)))
               (let loop ((xs files) (all-units '()))
                 (if (null? xs)
                     (let ((llvm (ll-units->llvm-module (reverse all-units))))
                       (if out
                           (call-with-output-file out
                             (lambda (p)
                               (display llvm p))
                             :if-exists :supersede)
                           (display llvm))
                       0)
                     (let ((units (frontend-compile-file (car xs) env)))
                       (loop (cdr xs) (append (reverse units) all-units)))))))))
      (else
       (usage)
       2))))

(exit (main (command-line)))
